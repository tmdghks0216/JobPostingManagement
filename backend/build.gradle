plugins {
	id 'org.springframework.boot' version '2.6.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java'
	// id 'war'   // WAR 필요 없으면 주석
	id "com.github.node-gradle.node" version "3.5.1"
}

java { toolchain { languageVersion = JavaLanguageVersion.of(8) } }

repositories { mavenCentral() }

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.jsoup:jsoup:1.17.2'
	implementation 'org.json:json:20231013'

	implementation 'mysql:mysql-connector-java:8.0.33'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}
test {
	useJUnitPlatform()
}


def frontendDir = file("${projectDir}/../frontend")
def staticDir   = file("${projectDir}/src/main/resources/static")

node {
	download = true
	version = "16.20.2"
	npmVersion = "8.19.4"
	nodeProjectDir = frontendDir
}

// npm run build 실행
tasks.register("npmBuild", NpmTask) {
	dependsOn("npmInstall")    // 플러그인 기본 제공
	args = ['run', 'build']
	workingDir = frontendDir
}

// static 폴더 통째로 삭제
tasks.register("cleanStatic", Delete) {
	delete staticDir
}

// frontend/build 를 static/ 으로 복사
tasks.register("copyFrontend", Copy) {
	dependsOn("npmBuild")
	from("${frontendDir}/build")
	into(staticDir)

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// processResources 이전에 실행
tasks.named("processResources") {
	dependsOn("cleanStatic", "copyFrontend")
}